*** Settings ***
Documentation     Example for file reader
Library           Collections
Library           FileLibrary.py

*** Variables ***
&{HEADER}   no=No    fruit=Fruit   price=Price    country=Country
&{APPLE}    no=1    fruit=Apple    price=10.00    country=Japan
&{ORANGE}   country=America    price=2.00    fruit=Orange    no=2
&{DURIAN}   country=Malaysia    price=15.00    fruit=Durian    no=3

&{APPLE_SHORT}    price=10.00    fruit=Apple
&{ORANGE_SHORT}   price=2.00    fruit=Orange
&{DURIAN_SHORT}   price=15.00    fruit=Durian


*** Test Cases ***
Verify that library can read all records in xlsx
  ${dictionary}=    Create Dictionary    A=no    B=fruit    C=country    D=price
  ${actual_record}=    read_multiple_records    ${CURDIR}/data/fruit.xlsx    ${dictionary}

  ${expected_record}=    Create List    ${APPLE}
  Append To List    ${expected_record}    ${ORANGE}
  Append To List    ${expected_record}    ${DURIAN}

  #assertion
  Lists Should Be Equal    ${actual_record}    ${expected_record}


Verify that library can read all records and header in xlsx
  ${dictionary}=    Create Dictionary    A=no    B=fruit    C=country    D=price
  ${actual_record}=    read_multiple_records    ${CURDIR}/data/fruit.xlsx    ${dictionary}    ${FALSE}

  ${expected_record}=    Create List    ${HEADER}
  Append To List    ${expected_record}    ${APPLE}
  Append To List    ${expected_record}    ${ORANGE}
  Append To List    ${expected_record}    ${DURIAN}

  #assertion
  Lists Should Be Equal    ${actual_record}    ${expected_record}


Verify that library can read the first record in xlsx
  ${dictionary}=    Create Dictionary    A=no    B=fruit    C=country    D=price
  ${actual_record}=    read_single_record    ${CURDIR}/data/fruit.xlsx    ${dictionary}

  ${expected_record}=    Create List    ${HEADER}

  #assertion
  Lists Should Be Equal    ${actual_record}    ${expected_record}


Verify that library can read any record in xlsx
  ${dictionary}=    Create Dictionary    A=no    B=fruit    C=country    D=price
  ${actual_record}=    read_single_record    ${CURDIR}/data/fruit.xlsx    ${dictionary}    3

  ${expected_record}=    Create List    ${ORANGE}

  #assertion
  Lists Should Be Equal    ${actual_record}    ${expected_record}


Verify that library can read multiple records from xlsx
  ${dictionary}=    Create Dictionary    B=fruit    D=price
  ${actual_record}=    read_multiple_records    ${CURDIR}/data/fruit.xlsx    ${dictionary}

  ${expected_record}=    Create List    ${APPLE_SHORT}
  Append To List    ${expected_record}    ${ORANGE_SHORT}
  Append To List    ${expected_record}    ${DURIAN_SHORT}

  #assertion
  Lists Should Be Equal    ${actual_record}    ${expected_record}


Verify that library can read single row from xlsx
  ${dictionary}=    Create Dictionary    A=no    B=fruit    C=country    D=price
  ${actual_list}=    read_single_row    ${CURDIR}/data/fruit.xlsx    ${dictionary}    4

  ${expected_list}=  Create List    3    Durian    Malaysia    15.00

  #assertion
  Lists Should Be Equal    ${actual_list}    ${expected_list}


Verify that library returns empty list from empty xlxs for read_multiple_records
  ${dictionary}=    Create Dictionary    B=fruit    D=price
  ${actual_record}=    read_multiple_records    ${CURDIR}/data/empty.xlsx    ${dictionary}

  ${expected_record}=    Create List

  #assertion
  Lists Should Be Equal    ${actual_record}    ${expected_record}


Verify that library returns empty list from empty xlxs for read_single_record
  ${dictionary}=    Create Dictionary    B=fruit    D=price
  ${actual_record}=    read_single_record    ${CURDIR}/data/empty.xlsx    ${dictionary}    3    ${FALSE}

  ${expected_record}=    Create List

  #assertion
  Lists Should Be Equal    ${actual_record}    ${expected_record}


Example on retrieving the attributes from list
  ${list}=    Create List    ${ORANGE}
  Append To List    ${list}    ${APPLE}
  Append To List    ${list}    ${DURIAN}

  #Get Item
  ${orange}=    Get From List    ${list}    0
  Dictionaries Should Be Equal    ${orange}    ${ORANGE}

  #Get Value
  ${country}=    Get From Dictionary    ${orange}    country
  Should Be Equal    ${country}    America


*** Keywords ***
